apply plugin: 'java'
apply plugin: 'maven'

description = "Satori SDK for Java with Jackson2 serializer."

sourceCompatibility = 1.6

repositories {
    mavenCentral()
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.project {
                name project.description
                description project.description
            }
        }
    }
}

task integration(type: Test, description: 'Runs the integration tests', group: 'verification') {
    dependsOn ':satori-rtm-sdk-core:integrationTestJar'
    testClassesDir = project(':satori-rtm-sdk-core').sourceSets.integration.output.classesDir
    classpath = sourceSets.test.runtimeClasspath
}

dependencies {
    compile project(':satori-rtm-sdk-core')

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.8'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.8'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.2'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    // needed to parse credentials.json
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    testCompile project(path: ':satori-rtm-sdk-core', configuration: 'integration')
}

check.dependsOn integration
